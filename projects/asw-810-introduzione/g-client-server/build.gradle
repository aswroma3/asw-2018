/* 
 * Gradle build file per un'applicazione Java multi-progetto
 * di tipo client-server. 
 * 
 * - gradle build compila, crea i jar e prepara la distribuzione 
 * - gradle clean cancella i jar file e la distribuzione 
 */ 
 
/* progetti i cui jar che devono essere rilasciati lato server o client */ 
def serverSideProjects = [':server', ':service']
def clientSideProjects = [':client', ':service']

subprojects {
    apply plugin: 'java'

    repositories {
        flatDir { 
           dirs "../../../libraries"
        }
        mavenCentral()
    }

    configurations {
        libs
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }
}

project(':service') {
}

project(':server') {
    
    dependencies {
        compile project(':service')
    }

    jar {
        manifest {
            attributes 'Main-Class': 'asw.intro.server.Server'
            attributes 'Class-Path': 'service.jar' 
        }
    }

    apply plugin: 'application' 
    mainClassName = "asw.intro.server.Server"
      
}

project(':client') {

    dependencies {
        compile project(':service')
    }

    jar {
        manifest {
            attributes 'Main-Class': 'asw.intro.client.Main'
            attributes 'Class-Path': 'service.jar' 
        }
    }

    apply plugin: 'application' 
    mainClassName = "asw.intro.client.Main"
 
}

/* 
 * DISTRIBUZIONE - SERVER 
 */ 

// copia nella cartella dist/server tutti i file di interesse  

task copyServerJarFiles(type: Copy, dependsOn: subprojects.build) {
    serverSideProjects.collect {
        project(it) { subproject -> 
            from subproject.buildDir  
            include 'libs/**'
        }
        into 'dist/server'
    }
}

// copia nella cartella dist i file jar delle librerie di interesse  
task copyServerLibFiles(type: Copy) {
    serverSideProjects.collect {
        project(it) { subproject -> 
            from subproject.configurations.libs  
            include '**'
        }
        into 'dist/server/libs'
    }
}

// copia nella cartella dist le altre risorse (script e file di configurazione) 
task copyServerResources(type: Copy) {
    from 'resources/server' 
    into 'dist/server'
}

task serverDist(dependsOn: [copyServerJarFiles, copyServerLibFiles, copyServerResources]) {
}

/* 
 * DISTRIBUZIONE - CLIENT 
 */ 

// copia nella cartella dist/client tutti i file di interesse  

task copyClientJarFiles(type: Copy, dependsOn: subprojects.build) {
    clientSideProjects.collect {
        project(it) { subproject -> 
            from subproject.buildDir  
            include 'libs/**'
        }
        into 'dist/client'
    }
}

// copia nella cartella dist i file jar delle librerie di interesse  
task copyClientLibFiles(type: Copy) {
    clientSideProjects.collect {
        project(it) { subproject -> 
            from subproject.configurations.libs  
            include '**'
        }
        into 'dist/client/libs'
    }
}

task copyClientResources(type: Copy) {
    from 'resources/client' 
    into 'dist/client'
}

task clientDist(dependsOn: [copyClientJarFiles, copyClientLibFiles, copyClientResources]) {
}

/* 
 * DISTRIBUZIONE e BUILD 
 */ 

task dist(dependsOn: [serverDist, clientDist]) {
}

task build(dependsOn: dist) {
}


/*
 * CLEAN 
 */ 

task deleteDist(type: Delete) {
    delete 'dist'
}

task clean(dependsOn: deleteDist) {
}

